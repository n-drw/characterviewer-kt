import cab.andrew.simpsonsview.Libs
import java.nio.file.Files

plugins {
	id 'com.android.application'
	id 'dagger.hilt.android.plugin'
	id 'com.github.triplet.play'

	id 'kotlin-android'
	id 'kotlin-kapt'

	id 'com.google.android.gms.oss-licenses-plugin'
	id 'kotlin-android-extensions'
}

play {
	track = 'internal'
	defaultToAppBundles = true
	serviceAccountCredentials = rootProject.file('release/play-account.json')
}

kapt {
	correctErrorTypes = true
	useBuildCache = true
}

def appVersionCode = propOrDef("simpsonsview.versioncode", 1) as Integer
println("APK version code: " + appVersionCode)

android {
	compileSdkVersion buildConfig.compileSdk
	buildToolsVersion "30.0.2"

	defaultConfig {
		applicationId "cab.andrew.simpsonsview"
		minSdkVersion buildConfig.minSdk
		targetSdkVersion buildConfig.targetSdk
		versionCode appVersionCode
		versionName "1.0"

		resConfigs "en"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		debug {
			versionNameSuffix "-dev"
			applicationIdSuffix 'cab.andrew.app.simpsonsview'
		}

		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles 'proguard-rules.pro'
			applicationIdSuffix 'cab.andrew.app.simpsonsview'
		}
	}

	buildFeatures {
		dataBinding true
		viewBinding true
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = '1.8'
	}

	packagingOptions {
		// Exclude AndroidX version files
		exclude 'META-INF/*.version'
		// Exclude consumer proguard files
		exclude 'META-INF/proguard/*'
		// Exclude the Firebase/Fabric/other random properties files
		exclude '/*.properties'
		exclude 'fabric/*.properties'
		exclude 'META-INF/*.properties'
	}

	testOptions {
		unitTests {
			includeAndroidResources = true
		}
	}

	flavorDimensions 'version'
	productFlavors {
		simpsons {
			dimension 'version'
			applicationId 'cab.andrew.app.simpsonsviewer'
			versionName 'Simpsons'
			buildConfigField("String", "URL", "\"https://api.duckduckgo.com/?q=simpsons+characters&format=json\"")
		}
		wire {
			dimension 'version'
			applicationId 'cab.andrew.app.wireviewer'
			versionName 'Wire'
			buildConfigField("String", "URL", "\"https://api.duckduckgo.com/?q=wire+characters&format=json\"")
		}
	}
	sourceSets {
		simpsonsDebug {
			res {
				srcDirs 'src/simpsonsDebug/res'
			}
		}
		wireDebug {
			res {
				srcDirs 'src/wire/res'
			}
		}
	}
}

dependencies {
	implementation project(path: ':base')
	implementation project(path: ':common-ui-view')
	implementation project(path: ':data')
	implementation project(path: ':navigation')
	implementation project(path: ':ui-about')
	implementation project(path: ':ui-discover')
	implementation project(path: ':ui-character-details')

	implementation Libs.AndroidX.Lifecycle.livedata
	implementation Libs.AndroidX.Lifecycle.viewmodel

	implementation Libs.AndroidX.appcompat
	implementation Libs.AndroidX.emoji
	implementation Libs.AndroidX.constraintlayout
	implementation Libs.AndroidX.Fragment.fragmentKtx
	implementation Libs.AndroidX.Navigation.fragment
	implementation Libs.AndroidX.Navigation.ui

	implementation Libs.Mdc.material

	implementation Libs.Insetter.ktx

	implementation Libs.timber

	implementation Libs.Kotlin.stdlib

	implementation Libs.Coroutines.core
	implementation Libs.Coroutines.android

	implementation Libs.Hilt.library
	implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
	implementation Libs.AndroidX.Room.runtime

	kapt Libs.Hilt.compiler
	implementation Libs.AndroidX.Hilt.work
	implementation Libs.AndroidX.Hilt.viewmodel
	kapt Libs.AndroidX.Hilt.compiler

	implementation Libs.Google.crashlytics
	implementation Libs.Google.openSourceLicensesLibrary

	debugImplementation Libs.leakCanary

	testImplementation Libs.junit
	testImplementation Libs.robolectric
	testImplementation Libs.AndroidX.Test.core
	testImplementation Libs.AndroidX.Test.rules
	testImplementation Libs.AndroidX.Test.espressoCore

	implementation Libs.Retrofit.retrofit
	implementation Libs.Retrofit.moshiConverter
	implementation Libs.moshi
	implementation Libs.moshiKt

	implementation Libs.koinAndroid
	implementation Libs.koinAndroidX
	implementation Libs.koinAndroidXViewModel
	implementation Libs.koinAndroidXFragment

	implementation Libs.glide
	kapt Libs.glideCompiler
}

